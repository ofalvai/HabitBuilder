import com.android.build.gradle.api.AndroidBasePlugin

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.android.tools.gradle
        classpath libs.kotlin.gradle
        classpath libs.licensee
        classpath libs.bugsnag.gradle
        classpath(libs.ruler)
    }
}

plugins {
    id "com.osacky.doctor" version "0.7.3"
    id "com.asarkar.gradle.build-time-tracker" version "3.0.1"
    id "com.google.devtools.ksp" version "1.6.0-1.0.1"
    id "org.gradle.android.cache-fix" version "2.4.5" apply false
}

doctor {
    javaHome {
        failOnError.set(false)
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" } // kotlinx artifacts required by Compose Runtime
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    plugins.withType(AndroidBasePlugin) {
        project.apply plugin: "org.gradle.android.cache-fix"
    }
}