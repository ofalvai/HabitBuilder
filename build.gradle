import com.android.build.gradle.api.AndroidBasePlugin

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.android.tools.gradle
        classpath libs.kotlin.gradle
        classpath libs.licensee
        classpath libs.bugsnag.gradle
        classpath libs.ruler
    }
}

plugins {
    alias(libs.plugins.ksp)
    alias(libs.plugins.gradle.doctor)
    alias(libs.plugins.gradle.buildtimetracker)
    alias(libs.plugins.gradle.cachefix) apply false
    alias(libs.plugins.dependencyanalysis)
}

doctor {
    javaHome {
        failOnError.set(false)
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://androidx.dev/snapshots/builds/8003490/artifacts/repository' } // TODO: temporary workaround for a Kotlin 1.6.10 compatible Compose compiler
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    plugins.withType(AndroidBasePlugin) {
        project.apply plugin: "org.gradle.android.cache-fix"
    }
}

dependencyAnalysis {
    issues {
        all {
            ignoreKtx(true)
            onUnusedDependencies {
                severity("fail")
                exclude(
                        "androidx.compose.ui:ui-tooling", // Required for IDE previews
                        "com.bugsnag:bugsnag-android", // False positive
                )
            }
            onIncorrectConfiguration {
                severity("fail")
            }
            onUnusedAnnotationProcessors {
                severity("fail")
            }
            onRedundantPlugins {
                severity("fail")
            }
        }
    }
}