plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

apply plugin: 'kotlin-kapt'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: 'app.cash.licensee'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.ofalvai.habittracker"
        minSdkVersion 22 // Trace minSdk is 22
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental": "true",
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }
    }

    lintOptions {
        enable "DuplicateStrings", "NewerVersionAvailable"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            signingConfig = debug.signingConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }

    packagingOptions {
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }
}

junitJacoco {
    jacocoVersion = "0.8.6"
    includeNoLocationClasses = true
}

afterEvaluate {
    tasks.withType(Test) {
        jacoco.excludes = ['jdk.internal.*']
    }
}

licensee {
    allow("Apache-2.0")
    allowUrl("http://www.apache.org/license/LICENSE-2.0.txt")
    allowDependency("com.github.kizitonwose", "CalendarView", "1.0.4")
}

// https://www.zacsweers.dev/kapts-hidden-test-costs/
tasks
    .matching {
        it.name.startsWith("kapt") && it.name.endsWith("TestKotlin")
    }
    .configureEach { it.enabled = false }

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'kotlinx-coroutines-core'
                && details.requested.version == '1.4.3') {
            details.useVersion '1.5.0'
            details.because 'fixes turbine runtime crash'
        }
    }
}

dependencies {
    coreLibraryDesugaring libs.android.tools.desugar

    implementation libs.bundles.compose
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.activity.compose
    implementation libs.androidx.lifecycle.viewmodel.compose

    implementation libs.androidx.room.ktx
    kapt libs.androidx.room.compiler

    implementation libs.timber
    implementation libs.calendarview
    implementation libs.accompanist.insets
    implementation libs.accompanist.systemuicontroller

    // Required by layout inspector
    debugImplementation libs.kotlin.reflect

    testImplementation libs.junit
    testImplementation libs.kotlin.coroutines.test
    testImplementation libs.mockito.core
    testImplementation libs.mockito.kotlin
    testImplementation libs.androidx.core.testing
    testImplementation libs.turbine

    androidTestImplementation libs.kotlin.coroutines.test
    androidTestImplementation libs.androidx.junit.ext
    androidTestImplementation libs.androidx.espresso
    androidTestImplementation libs.androidx.core.testing
    androidTestImplementation libs.turbine
}