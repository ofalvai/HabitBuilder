plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.spotify.ruler'
    id 'habittracker.android.app'
}

apply plugin: 'com.google.devtools.ksp'
apply plugin: 'app.cash.licensee'
apply plugin: 'com.bugsnag.android.gradle'

ext {
    PACKAGE_NAME = "com.ofalvai.habittracker"
    API_KEY_BUGSNAG = "2e13a542ae3ed09ad42a5c82444925af"
}

def useReleaseKeystore = rootProject.file("release/app-release.jks").exists()

android {
    defaultConfig {
        applicationId PACKAGE_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [api_key_bugsnag: "${API_KEY_BUGSNAG}"]
    }

    lint {
        enable 'DuplicateStrings', 'NewerVersionAvailable'
    }
    lintOptions {
        // Ignore any tests
        ignoreTestSources true
        // Make the build fail on any lint errors
        abortOnError true
    }

    signingConfigs {
        release {
            if (useReleaseKeystore) {
                storeFile rootProject.file("release/app-release.jks")
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            if (useReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                // Otherwise just use the debug keystore (this is mainly for PR CI builds)
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        useIR = true
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
        freeCompilerArgs += '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
    }
    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
         kotlinCompilerExtensionVersion libs.versions.compose.get()
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/licenses/**']
        }
        resources {
            excludes += ['META-INF/licenses/**', 'META-INF/AL2.0', 'META-INF/LGPL2.1', '**/attach_hotspot_windows.dll']
        }
    }

    testOptions {
        animationsDisabled = true
    }
}

ruler {
    abi.set("arm64-v8a")
    locale.set("en")
    screenDensity.set(480)
    sdkVersion.set(27)
}

licensee {
    allow("Apache-2.0")
    allow("MIT")
    allowDependency("com.github.kizitonwose", "CalendarView", "1.0.4") {
        because "Artifact declares no licenses, but it's MIT according to GitHub"
    }
}

dependencies {
    implementation project(':core-database')
    implementation project(':core-ui')
    implementation project(':core-model')
    coreLibraryDesugaring libs.android.tools.desugar

    implementation libs.bundles.compose
    implementation libs.compose.ui.toolingpreview
    debugImplementation libs.compose.ui.tooling

    implementation libs.kotlin.coroutines.core

    implementation libs.androidx.navigation.compose
    implementation libs.androidx.activity.compose

    implementation libs.androidx.room.runtime

    implementation libs.logcat
    implementation libs.calendarview
    implementation(libs.reorderable)
    implementation libs.accompanist.insets
    implementation libs.accompanist.systemuicontroller
    implementation libs.accompanist.navigation

    implementation libs.bugsnag.runtime

    // Required by layout inspector
    debugRuntimeOnly libs.kotlin.reflect

    implementation libs.showkase.annotation
    debugImplementation libs.showkase.full
    kspDebug libs.showkase.processor

    testImplementation project(':core-testing')
    testImplementation libs.junit
    testImplementation libs.kotlin.coroutines.test
    testImplementation libs.mockito.kotlin
    testImplementation libs.turbine

    androidTestImplementation project(':core-testing')
    androidTestImplementation libs.kotlin.coroutines.test
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.junit.ext
    androidTestImplementation libs.androidx.espresso
    androidTestImplementation libs.androidx.core.testing
    androidTestImplementation libs.turbine
    androidTestImplementation libs.compose.ui.test
    debugImplementation libs.compose.ui.test.manifest
}